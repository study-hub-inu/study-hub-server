//querydsl 추가
buildscript {
    dependencies {
        classpath("gradle.plugin.com.ewerk.gradle.plugins:querydsl-plugin:1.0.10")
    }
}

plugins {
    id 'java'
    id 'org.springframework.boot' version '2.7.14'
    id 'io.spring.dependency-management' version '1.0.15.RELEASE'
    id 'jacoco'
}

apply plugin: "com.ewerk.gradle.plugins.querydsl"

group = 'kr.co.study-hub-appcenter'
version = '0.0.1-SNAPSHOT'

java {
    sourceCompatibility = '11'
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-web-services'
    implementation 'mysql:mysql-connector-java:8.0.26'
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'
    developmentOnly 'org.springframework.boot:spring-boot-devtools'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'


    // swagger
    implementation 'io.springfox:springfox-boot-starter:3.0.0'
    implementation 'io.springfox:springfox-swagger-ui:3.0.0'
    // DB
    runtimeOnly 'com.h2database:h2'
    runtimeOnly 'mysql:mysql-connector-java'
    // jwt
    implementation 'com.auth0:java-jwt:4.0.0'
    // mail
    implementation 'org.springframework.boot:spring-boot-starter-mail'
    // redis
    implementation 'org.springframework.boot:spring-boot-starter-data-redis'
    //s3
    implementation 'org.springframework.cloud:spring-cloud-starter-aws:2.2.6.RELEASE'

    // queryDsl
    implementation 'com.querydsl:querydsl-jpa'
    implementation 'com.querydsl:querydsl-apt'


    // 직렬화 문제 해결
    implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310'
    implementation 'com.fasterxml.jackson.core:jackson-databind'

    //Spring Batch
    implementation 'org.springframework.boot:spring-boot-starter-batch'
    testImplementation 'org.springframework.batch:spring-batch-test'

    // firebase
    implementation 'com.google.firebase:firebase-admin:9.2.0'

    // sentry
    implementation 'io.sentry:sentry-spring-boot-starter:7.3.0'
    implementation 'io.sentry:sentry-logback:5.3.0'
}

//querydsl 추가
//def querydslDir = 'src/main/generated'
def querydslDir = "$buildDir/generated/querydsl"

querydsl {
    library = "com.querydsl:querydsl-apt"
    jpa = true
    querydslSourcesDir = querydslDir
}
sourceSets {
    main {
        java {
            srcDirs = ['src/main/java', querydslDir]
        }
    }
}
compileQuerydsl{
    options.annotationProcessorPath = configurations.querydsl
}
configurations {
    querydsl.extendsFrom compileClasspath
}


tasks.named('test') {
    useJUnitPlatform()
    finalizedBy jacocoTestReport // 테스트 종료후 항상 리포트 생성
}

jacoco {
    toolVersion = "0.8.8"
}

jacocoTestReport {
    dependsOn test
    reports {
        xml.required.set(true)
    }
    def Qdomains = []
    for(qPattern in "**/QA" .. "**/QZ"){
        Qdomains.add(qPattern+"*")
    }

    afterEvaluate {

        classDirectories.setFrom(files(classDirectories.files.collect { dir ->
            fileTree(dir: dir, exclude: Qdomains + [
                    '**/*Request*',
                    '**/*Response*',
                    '**/SwaggerConfig*',
                    '**/BatchConfig*',
                    '**/RedisConfig*',
                    '**/StudyPostJobConfig*',
                    '**/CorsConfig*',
                    '**/S3Config*',
                    '**/QuerydslConfig*',
                    '**/RestTemplateConfig*',
                    '**/defaultController*',
                    '**/RestTemplateConfig*',
                    '**/CustomResponseUtil*',
                    '**/QueryStringArgumentResolver*',
                    '**/UserIdArgumentResolver*',
                    '**/TimerAop*',
                    '**/JpaAuditingConfig*'
            ])
        }))
    }

    finalizedBy 'jacocoTestCoverageVerification'
}

jar {
    enabled = false
}

jacocoTestCoverageVerification {
    def Qdomains = []
    for (qPattern in "*.QA".."*.QZ") {  // qPattern = "*.QA","*.QB","*.QC", ... "*.QZ"
        Qdomains.add(qPattern + "*")
    }

    violationRules {
        rule {
            enabled = true
            element = 'CLASS'
            excludes = [] + Qdomains

        }

    }
}